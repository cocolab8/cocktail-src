/* Ich, Doktor Josef Grosch, Informatiker, 4.3.1992 */

TRAFO EvalMod3
TREE Tree
PUBLIC EvalImplMod EvalDefMod ToBit0

GLOBAL {

FROM SYSTEM	IMPORT ADR, TSIZE;
FROM General	IMPORT Max;
FROM DynArray	IMPORT MakeArray;
FROM IO		IMPORT WriteS, WriteNl, WriteI, WriteB, StdOutput;
FROM Idents	IMPORT tIdent, MaxIdent;
FROM Texts	IMPORT WriteText;
FROM Sets	IMPORT tSet, MakeSet, ReleaseSet, Include, Exclude, Minimum,
			Maximum, IsElement, WriteSet, IsEmpty, Extract;
FROM Relation	IMPORT IsRelated;
FROM TreeMod1	IMPORT BSS;
FROM TreeMod2	IMPORT GetIterator, Iterator, WriteLine;
FROM EvalMod	IMPORT Class;
FROM Errors	IMPORT Error, Short, MessageI;
FROM Position	IMPORT NoPosition;
IMPORT EvalMod;

FROM Tree	IMPORT
   NoTree	, tTree		, NoCodeClass	, Options	,
   Computed	, Reverse	, Write		, Read		,
   Inherited	, Synthesized	, Input		, Output	,
   Virtual	, Test		, Left		, Right		,
   HasOutput	, NonBaseComp	, Dummy		, Trace		,
   TreeRoot	, iMain		, itTree	, ErrorCount	,
   ForallClasses, ForallAttributes, f		, WI	, WN	,
   ClassCount	, IdentifyClass	, IdentifyAttribute, 
   tBitIndex	, tBitInfo	, iNoTree	, QueryTree	;

VAR
   i, i2, j, k, n, MaxBit, MaxInstCount, Check: SHORTCARD;
   Node, Attr		: tTree;
   Success, IsStable, IsFirst	: BOOLEAN;
   BitIndexSize		: LONGINT;
   gBitIndex		: tBitIndex;
   InhIndices, EvalNames: tSet;
   InhIndexSize		: LONGINT;
   InhIndexCount	: POINTER TO ARRAY [1..1000000] OF SHORTCARD;
   gIdent		: tIdent;

PROCEDURE GenCall (t: tTree; j: SHORTCARD);
   BEGIN
      WITH t^.Class.Instance^ [j] DO
	 IF ({Synthesized, Left} <= Properties) THEN
	    k := ToBit0 (t, j);
	    !IFNOTIN (! WN (k MOD BSS); !, yyt^.yyHead.yyIsComp! WN (k DIV BSS); !) ! 
	    !yyS! WN (k); ! (yyt); (* ! WI (Attribute^.Child.Name); ! *) END;!
	 ELSIF ({Inherited, Left} <= Properties) THEN
	    k := ToBit0 (t, j);
	    !IFNOTIN (! WN (k MOD BSS); !, yyt^.yyHead.yyIsComp! WN (k DIV BSS); !) ! 
   IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	    !yyVisitParent (yyt); ! 
	    !yyI [yyt^.yyHead.yyOffset + ! WN (k); !](yyt^.yyHead.yyParent); (* ! 
	    WI (Attribute^.Child.Name); ! *) ! 
	    @yyWriteVisit (yyt^.yyHead.yyParent, "?"); END;@
   ELSE
	    !yyI [yyt^.yyHead.yyOffset + ! WN (k); !](yyt^.yyHead.yyParent); (* ! 
	    WI (Attribute^.Child.Name); ! *) END;!
   END;
	 ELSIF ({Inherited, Right} <= Properties) THEN
	    k := ToBit1 (Selector, j - t^.Class.AttrCount - Selector^.Child.InstOffset);
	    !IFNOTIN (! WN (k MOD BSS); !, yyt^.! WI (Class^.Class.Name);
	    !.! WI (Selector^.Child.Name); !^.yyHead.yyIsComp! WN (k DIV BSS); !) ! 
	    k := ToBit2 (t, Selector, j);
	    !yyI! WN (k); ! (yyt); (* ! WI (Selector^.Child.Name);
	    !:! WI (Attribute^.Child.Name); ! *) END;!
	 ELSIF ({Synthesized, Right} <= Properties) THEN
	    k := ToBit1 (Selector, j - t^.Class.AttrCount - Selector^.Child.InstOffset);
	    !IFNOTIN (! WN (k MOD BSS); !, yyt^.! WI (Class^.Class.Name);
	    !.! WI (Selector^.Child.Name); !^.yyHead.yyIsComp! WN (k DIV BSS); !) ! 
   IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	    @yyWriteVisit (yyt, "@ WI (Selector^.Child.Name); @"); @ 
	    !yyS! WN (k);
	    ! (yyt^.! WI (Class^.Class.Name); !.! WI (Selector^.Child.Name);
	    !); (* ! WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); ! *) ! 
	    !yyVisitParent (yyt^.! WI (Class^.Class.Name); !.! WI (Selector^.Child.Name); !); END;!
   ELSE
	    !yyS! WN (k);
	    ! (yyt^.! WI (Class^.Class.Name); !.! WI (Selector^.Child.Name);
	    !); (* ! WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); ! *) END;!

   END;
	 END;
      END;
   END GenCall;

PROCEDURE GenCompAttr (t: tTree; i: INTEGER);
   BEGIN
      Class := t;
      WITH t^.Class.Instance^ [i] DO
   IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
	 @yyWriteEval (yyt, "@ WI (Attribute^.Child.Name); @");@
	 IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
	    GenEvaluator (Action);
	    EvalMod.GenEvaluator (Action); !!
	    IF Test IN Properties THEN
	       !writeBOOLEAN (yyb) yyWriteNl;!
	    ELSIF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
	       !write! WI (itTree);
	       ! (yyt^.! WI (t^.Class.Name); !.! WI (Attribute^.Child.Name); !) yyWriteNl;!
	    ELSE
	       !write! WI (Attribute^.Child.Type);
	       ! (yyt^.! WI (t^.Class.Name); !.! WI (Attribute^.Child.Name); !) yyWriteNl;!
	    END;
	 ELSE
	    !yyWriteNl;!
	 END;
   ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
	 @yyWriteEval (yyt, "@ WI (Attribute^.Child.Name); @");@
	 IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
	    GenEvaluator (Action);
	    EvalMod.GenEvaluator (Action);
	 END;
   ELSE
	 IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
	    GenEvaluator (Action);
	    EvalMod.GenEvaluator (Action);
	 END;
   END;
      END;
   END GenCompAttr;
}

PROCEDURE EvalDefMod (t: Tree)
	
Ag (..) :- {
	!DEFINITION MODULE ! WI (EvalName); !;!
	!!
	!IMPORT ! WI (iMain); !;!
	WriteLine (EvalCodes^.Codes.ImportLine);
	WriteText (f, EvalCodes^.Codes.Import);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.ImportLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Import);
	   Node := Node^.Module.Next;
	END;
	WriteLine (EvalCodes^.Codes.ExportLine);
	WriteText (f, EvalCodes^.Codes.Export);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.ExportLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Export);
	   Node := Node^.Module.Next;
	END;
	!!
	!PROCEDURE Begin! WI (EvalName); !;!
	!PROCEDURE Eval (yyt: ! WI (iMain); !.! WI (itTree); !);!
	MakeSet (EvalNames, MaxIdent ());
	ForallClasses (Classes, GenEvalAttrD);
	ReleaseSet (EvalNames);
	!PROCEDURE Close! WI (EvalName); !;!
	!!
	!END ! WI (EvalName); !.!
}; .

PROCEDURE EvalImplMod (t: Tree)
	
Ag (..) :- {
	MaxBit := 0;
	MaxInstCount := 0;
	ForallClasses (Classes, CompBitInfo);
	MakeSet (InhIndices, MaxInstCount);
	InhIndexSize := MaxInstCount;
	MakeArray (InhIndexCount, InhIndexSize, TSIZE (SHORTCARD));
	FOR i := 1 TO MaxInstCount DO InhIndexCount^ [i] := 0; END;
	ForallClasses (Classes, CompInhIndices);
	!# define IFNOTIN(b, s) IF NOT (b IN s) THEN!
	!# define REMOTE_SYN(i, b, c, n, t, a) n^.t.a!
	!# define REMOTE_INH(i, b, k, n, t, a) n^.t.a!
	EvalMod.EvalImplHead (t);
	!CONST yyCheck2 = ! WI (iMain); !.Generate! WI (iMain); !ModuleWithOptionL;!
	!!
	!VAR yyI: ARRAY [0..! WN (Maximum (InhIndices)); !] OF ! WI (iMain); !.tProcTree;!
	!!
	!PROCEDURE yyAbort (yyt: ! WI (iMain); !.! WI (itTree); !);!
	! BEGIN!
	!  IO.WriteS (IO.StdError, 'Error: module ! WI (EvalName); !, cyclic dependencies');!
	!  IO.WriteNl (IO.StdError);!
	!  IO.CloseIO;!
	!  ! WI (iMain); !.yyExit;!
	! END yyAbort;!
	!!
      IF NOT IsElement (ORD ('9'), Options) THEN
	!!
	!PROCEDURE Eval (yyt: ! WI (iMain); !.! WI (itTree); !);!
	! BEGIN ! WI (iMain); !.Init! WI (iMain); ! (yyt); yyE (yyt); END Eval;!
      ELSE
	!!
	!VAR yyMinLow: SYSTEM.ADDRESS;!
	!!
	!PROCEDURE Eval (yyt: ! WI (iMain); !.! WI (itTree); !);!
	! VAR yyHigh: BOOLEAN;!
	! BEGIN!
	!  yyMinLow := SYSTEM.ADR (yyHigh);!
	!  ! WI (iMain); !.Init! WI (iMain); ! (yyt); yyE (yyt);!
	!  IO.WriteS (IO.StdOutput, 'Stacksize ');!
	!  IO.WriteI (IO.StdOutput, Pointers.PTRDIFF (SYSTEM.ADR (yyHigh), yyMinLow), 0);!
	!  IO.WriteNl (IO.StdOutput);!
	! END Eval;!
      END;
	!!
	REPEAT IsStable := TRUE; ForallClasses (Classes, CompOutput); UNTIL IsStable;
	!PROCEDURE yyE (yyt: ! WI (iMain); !.! WI (itTree); !);!
	WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	Node := TreeRoot^.Ag.Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
	   Node := Node^.Module.Next;
	END;
      IF IsElement (ORD ('9'), Options) THEN
	! VAR yyLow: BOOLEAN;!
	! BEGIN!
	!  IF Pointers.PTRDIFF (yyMinLow, SYSTEM.ADR (yyLow)) > 0 THEN!
	!   yyMinLow := SYSTEM.ADR (yyLow);!
	!  END;!
      ELSE
	! BEGIN!
      END;
	!  LOOP!
	!   IF (yyt = ! WI (iMain); !.! WI (iNoTree); !) OR (0 IN yyt^.yyHead.yyIsComp0) THEN RETURN; END;!
	!   INCL (yyt^.yyHead.yyIsComp0, 0);!
	!   CASE yyt^.Kind OF!
	ForallClasses (Classes, GenE);
	!   ELSE RETURN;!
	!   END;!
	!  END;!
	! END yyE;!
	!!
	MakeSet (EvalNames, MaxIdent ());
	ForallClasses (Classes, GenEvalAttrI);
	ReleaseSet (EvalNames);
	FOR i := 2 TO MaxBit DO
	   n := 0;			(* are there any SYN attributes ? *)
	   ForallClasses (Classes, CountSynAttr);
	   IF n > 0 THEN
	      !PROCEDURE yyS! WN (i - 1); ! (yyt: ! WI (iMain); !.! WI (itTree); !);!
	      WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	      WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	      Node := TreeRoot^.Ag.Modules;
	      WHILE Node^.Kind = Tree.Module DO
		 WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
		 WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
		 Node := Node^.Module.Next;
	      END;
      IF IsElement (ORD ('9'), Options) THEN
	      ! VAR yyLow: BOOLEAN;!
	      ! BEGIN!
	      !  IF Pointers.PTRDIFF (yyMinLow, SYSTEM.ADR (yyLow)) > 0 THEN!
	      !   yyMinLow := SYSTEM.ADR (yyLow);!
	      !  END;!
      ELSE
	      ! BEGIN!
      END;
      IF IsElement (ORD ('5'), Options) THEN
	      ! IFNOTIN (! WN ((i - 1) MOD BSS); !, yyt^.yyHead.yyIsDone! WN ((i - 1) DIV BSS);
	      !) INCL (yyt^.yyHead.yyIsDone! WN ((i - 1) DIV BSS); !, ! WN ((i - 1) MOD BSS); !); ELSE yyAbort (yyt); END;!
      END;
	      IF n > 1 THEN
		 !  CASE yyt^.Kind OF!
		 ForallClasses (Classes, GenS);
		 !  END;!
	      ELSE
		 ForallClasses (Classes, GenS);
	      END;
	      !  INCL (yyt^.yyHead.yyIsComp! WN ((i - 1) DIV BSS); !, ! WN ((i - 1) MOD BSS); !);!
	      ! END yyS! WN (i - 1); !;!
	      !!
	   END;
	END;
	FOR i := Minimum (InhIndices) TO Maximum (InhIndices) DO
	   IF IsElement (i, InhIndices) THEN
	      !PROCEDURE yyI! WN (i); ! (yyt: ! WI (iMain); !.! WI (itTree); !);!
	      WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	      WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	      Node := TreeRoot^.Ag.Modules;
	      WHILE Node^.Kind = Tree.Module DO
		 WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
		 WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
		 Node := Node^.Module.Next;
	      END;
      IF IsElement (ORD ('9'), Options) THEN
	      ! VAR yyLow: BOOLEAN;!
	      ! BEGIN!
	      !  IF Pointers.PTRDIFF (yyMinLow, SYSTEM.ADR (yyLow)) > 0 THEN!
	      !   yyMinLow := SYSTEM.ADR (yyLow);!
	      !  END;!
      ELSE
	      ! BEGIN!
      END;
	      Check := 0;
	      IF InhIndexCount^ [i] > 1 THEN
		 !  CASE yyt^.Kind OF!
		 ForallClasses (Classes, EvalImplMod);
		 !  END;!
	      ELSE
		 ForallClasses (Classes, EvalImplMod);
	      END;
	      IF Check # InhIndexCount^ [i] THEN
	         MessageI ("internal error in yyI", Error, NoPosition, Short, ADR (i));
		 INC (ErrorCount);
	      END;
	      ! END yyI! WN (i); !;!
	      !!
	   END;
	END;
	!PROCEDURE Begin! WI (EvalName); !;!
	! BEGIN!
	WriteLine (EvalCodes^.Codes.BeginLine);
	WriteText (f, EvalCodes^.Codes.Begin);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.BeginLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Begin);
	   Node := Node^.Module.Next;
	END;
	! END Begin! WI (EvalName); !;!
	!!
	!PROCEDURE Close! WI (EvalName); !;!
	! BEGIN!
	WriteLine (EvalCodes^.Codes.CloseLine);
	WriteText (f, EvalCodes^.Codes.Close);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.CloseLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Close);
	   Node := Node^.Module.Next;
	END;
	! END Close! WI (EvalName); !;!
	!!
	!BEGIN!
      IF IsElement (ORD ('X'), Options) THEN
	! yyf := IO.StdOutput;!
      END;
	FOR i := Minimum (InhIndices) TO Maximum (InhIndices) DO
	   IF IsElement (i, InhIndices) THEN
	      ! yyI [! WN (i); !] := yyI! WN (i); !;!
	   END;
	END;
	!END ! WI (EvalName); !.!
}; .
Class (..) :-
	CONDITION NoCodeClass * Properties = {{}};
	CONDITION i <= InstCount;
	a: SHORTCARD;
{	a := ToAttr (t, i);
	IF a = 0 THEN RETURN; END;
	WITH Instance^ [a] DO
	   IF {Inherited, Right} <= Properties THEN
	      Class := t;
	      IF InhIndexCount^ [i] > 1 THEN
		 !   | ! WI (iMain); !.! WI (Name); !:!
	      END;
	      INC (Check);
	      k := ToBit1 (Selector, a - AttrCount - Selector^.Child.InstOffset);
      IF IsElement (ORD ('5'), Options) THEN
	      ! IFNOTIN (! WN (k MOD BSS); !, yyt^.! WI (Class^.Class.Name);
	      !.! WI (Selector^.Child.Name); !^.yyHead.yyIsDone! WN (k DIV BSS);
	      !) INCL (yyt^.! WI (Class^.Class.Name); !.! WI (Selector^.Child.Name);
	      !^.yyHead.yyIsDone! WN (k DIV BSS); !, ! WN (k MOD BSS); !); ELSE yyAbort (yyt); END;!
      END;
	      FOR j := 1 TO InstCount DO
		 IF IsRelated (a, j, DP) THEN
		    GenCall (t, j);
		 END;
	      END;
      IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
	      @yyWriteEval (yyt, "@ WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); @");@
	      IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
		 GenEvaluator (Action);
		 EvalMod.GenEvaluator (Action); !!
		 IF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
		    !write! WI (itTree);
		 ELSE
		    !write! WI (Attribute^.Child.Type);
		 END;
		 ! (yyt^.! WI (Name); !.! WI (Selector^.Child.Name);
		 !^.! WI (Selector^.Child.Type); !.! WI (Attribute^.Child.Name); !) yyWriteNl;!
	      ELSE
		 !yyWriteNl;!
	      END;
      ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
	      @yyWriteEval (yyt, "@ WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); @");@
	      IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
		 GenEvaluator (Action);
		 EvalMod.GenEvaluator (Action);
	      END;
      ELSE
	      IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
		 GenEvaluator (Action);
		 EvalMod.GenEvaluator (Action);
	      END;
      END;
	      IF NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) THEN
		 !WITH yyt^.! WI (Name); !.! WI (Selector^.Child.Name);
		 !^.! WI (Selector^.Child.Type); !.! WI (Attribute^.Child.Name);
		 !^.yyHead DO IF yyParent = ! WI (iMain); !.! WI (iNoTree); ! THEN yyOffset := ! 
		 WN (Selector^.Child.Class^.Class.BitCount + Attribute^.Child.BitOffset);
		 !; yyParent := yyt^.! WI (Name); !.! WI (Selector^.Child.Name); !; !
		 WI (iMain); !.Init! WI (iMain); ! (yyt^.! WI (Name); !.! WI (Selector^.Child.Name); 
		 !^.! WI (Selector^.Child.Type); !.! WI (Attribute^.Child.Name); !); END; END;!
	      END;
	      FOR i2 := 1 TO InstCount DO	(* add group members *)
		 IF Instance^[i2].Action = Action THEN
		    WITH Instance^[i2] DO
		       IF Synthesized IN Properties THEN
			  k := ToBit0 (Class, i2);
			  !   INCL (yyt^.yyHead.yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
		       ELSIF Inherited IN Properties THEN
			  k := ToBit1 (Selector, i2 - AttrCount - Selector^.Child.InstOffset);
			  !   INCL (yyt^.! WI (Class^.Class.Name); !.! WI (Selector^.Child.Name);
			  !^.yyHead.yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
		       END;
		    END;
		 END;
	      END;
	   END;
	END;
}; .

PROCEDURE CompBitInfo (t: Tree)

Class (..) :-
	BitIndexSize := AttrCount;
	MakeArray (BitIndex, BitIndexSize, TSIZE (tBitInfo));
	i := 1;
	gBitIndex := BitIndex;
	ForallAttributes (t, CompBitInfo);
	MaxBit := Max (i, MaxBit);
	MaxInstCount := Max (InstCount, MaxInstCount);
	.
Child (..) ;
Attribute (..) :-
	CONDITION {{Input, Test, Dummy}} * Properties = {{}};
	INC (i);
	gBitIndex^ [AttrIndex].ToBit := i;
	gBitIndex^ [i].ToAttr := AttrIndex;
	.

PROCEDURE CompInhIndices (t: Tree)

Class (..) :-
	b: INTEGER;
{	FOR j := AttrCount + 1 TO InstCount DO
	   WITH Instance^ [j] DO
	      IF Inherited IN Properties THEN
		 b := ToBit2 (t, Selector, j);
		 Include (InhIndices, b);
		 INC (InhIndexCount^ [b]);
	      END;
	   END;
	END;
};	.

PROCEDURE CountSynAttr (t: Tree)

Class (..) :-
	CONDITION NoCodeClass * Properties = {{}};
	CONDITION i <= BitCount;
{	WITH Instance^ [BitIndex^ [i].ToAttr] DO
	   IF ({Synthesized, Left} <= Properties) AND NOT (Test IN Properties) THEN
	      INC (n);
	   END;
	END;
};	.

PROCEDURE GenS (t: Tree)

Class (..) :-
	CONDITION NoCodeClass * Properties = {{}};
	CONDITION i <= BitCount;
{	WITH Instance^ [BitIndex^ [i].ToAttr] DO
	   IF ({Synthesized, Left} <= Properties) AND NOT (Test IN Properties) THEN
	      Class := t;
	      IF n > 1 THEN
		 !   | ! WI (iMain); !.! WI (Name); !:!
	      END;
	      FOR j := 1 TO InstCount DO
		 IF IsRelated (BitIndex^ [i].ToAttr, j, DP) THEN
		    GenCall (t, j);
		 END;
	      END;
	      GenCompAttr (t, BitIndex^ [i].ToAttr);
	      IF NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) THEN
		 !WITH yyt^.! WI (Name); !.! WI (Attribute^.Child.Name);
		 !^.yyHead DO IF yyParent = ! WI (iMain); !.! WI (iNoTree); ! THEN yyOffset := ! 
		 WN (BitCount + Attribute^.Child.BitOffset); !; yyParent := yyt; ! 
		 WI (iMain); !.Init! WI (iMain); ! (yyt^.! WI (Name); !.! WI (Attribute^.Child.Name); !); END; END;!
	      END;
	      FOR i2 := 1 TO InstCount DO	(* add group members *)
		 IF Instance^[i2].Action = Action THEN
		    WITH Instance^[i2] DO
		       IF Synthesized IN Properties THEN
			  k := ToBit0 (Class, i2);
			  IF k # i - 1 THEN
			     !   INCL (yyt^.yyHead.yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
			  END;
		       ELSIF Inherited IN Properties THEN
			  k := ToBit1 (Selector, i2 - AttrCount - Selector^.Child.InstOffset);
			  !   INCL (yyt^.! WI (Class^.Class.Name); !.! WI (Selector^.Child.Name);
			  !^.yyHead.yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
		       END;
		    END;
		 END;
	      END;
	   END;
	END;
}; .

PROCEDURE GenE (t: Tree)

Class (..) :-
	ToCompute: tSet;
{	GetIterator (t);
	n := 0;
	j := 2;
	LOOP
	   IF j > InstCount THEN EXIT; END;
	   WITH Instance^ [j] DO
	      IF {Dummy, Output, Test} * Properties # {} THEN
	         IF (Test IN Properties) OR
		    ({Synthesized, Left} <= Properties) OR
		    ({Inherited,  Right} <= Properties) OR
		    ({Inherited,   Left} <= Properties) AND
		    NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) OR
		    ({Synthesized, Right, Dummy} <= Properties) AND (Selector # Iterator) AND
		    (HasOutput IN Selector^.Child.Class^.Class.Properties) THEN
		    INC (n); EXIT;
		 END;
	      END;
	   END;
	   INC (j);
	END;
	IF (n = 0) AND ((Iterator = NoTree) OR NOT (HasOutput IN Iterator^.Child.Class^.Class.Properties)) THEN RETURN; END;

	Class := t;
	!   | ! WI (iMain); !.! WI (Name); !:!
	FOR j := 2 TO InstCount DO
	   WITH Instance^ [j] DO
	      IF {Dummy, Output} * Properties # {} THEN
		 IF ({Synthesized, Left} <= Properties) OR
		    ({Inherited,  Right} <= Properties) OR
		    ({Inherited,   Left} <= Properties) AND
		    NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) THEN
		    GenCall (t, j);
		 ELSIF ({Synthesized, Right, Dummy} <= Properties) AND (Selector # Iterator) AND
		    (HasOutput IN Selector^.Child.Class^.Class.Properties) THEN
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
		    !yyWriteVisit (yyt, '! WI (Selector^.Child.Name); !'); ! 
      END;
		    !yyE (yyt^.! WI (Name); !.! WI (Selector^.Child.Name); !);!
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
		    !yyVisitParent (yyt^.! WI (Name); !.! WI (Selector^.Child.Name); !);!
      END;
		 END;
	      END;
	   END;
	END;

	MakeSet (ToCompute, InstCount);
	FOR i := 2 TO AttrCount DO
	   WITH Instance^ [i] DO
	      IF Test IN Properties THEN
		 FOR j := 2 TO InstCount DO
		    IF IsRelated (i, j, DP) THEN
		       IF {Synthesized, Inherited} * Instance^ [j].Properties # {} THEN
			  Include (ToCompute, j);
		       END;
		    END;
		 END;
	      END;
	   END;
	END;
	FOR i := 2 TO InstCount DO
	   WITH Instance^ [i] DO
	      IF ({Synthesized, Left, Output} <= Properties) OR
		 ({Inherited,  Right, Output} <= Properties) THEN
		 Exclude (ToCompute, i);
	      END;
	   END;
	END;
	WHILE NOT IsEmpty (ToCompute) DO
	   GenCall (t, Extract (ToCompute));
	END;
	ReleaseSet (ToCompute);
	FOR i := 2 TO AttrCount DO
	   IF Test IN Instance^ [i].Properties THEN
	      GenCompAttr (t, i);
	   END;
	END;

	IF (Iterator = NoTree) OR NOT (HasOutput IN Iterator^.Child.Class^.Class.Properties) THEN
	   !RETURN;!
	ELSE
   IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	   !yyWriteVisit (yyt, '! WI (Iterator^.Child.Name); !'); ! 
   END;
	   !yyt := yyt^.! WI (Name); !.! WI (Iterator^.Child.Name); !;!
	END;
}; .

PROCEDURE CompOutput (t: Tree)

Class (..) :-
   CONDITION NOT (HasOutput IN Properties);
   Success := FALSE;
   ForallAttributes (t, CompOutput);
   ForallClasses (Extensions, CompOutput2);
   CONDITION Success;
   INCL (Properties, HasOutput);
   IsStable := FALSE;
   .
Child (..) :-
   CONDITION (Output IN Properties) OR (HasOutput IN Class^.Class.Properties);
   Success := TRUE;
   .
Attribute (..) :-
   CONDITION {{Test, Output}} * Properties # {{}};
   Success := TRUE;
   .

PROCEDURE CompOutput2 (t: Tree)

Class (..) :-
   CONDITION HasOutput IN Properties;
   Success := TRUE;
   .

FUNCTION ToBit0 (Class, INTEGER) INTEGER
   class, i :- RETURN class^.Class.BitIndex^ [i].ToBit - 1; .

FUNCTION ToBit1 (Child, INTEGER) INTEGER
   Selector, i :- RETURN Selector^.Child.Class^.Class.BitIndex^ [i].ToBit - 1; .

FUNCTION ToBit2 (Class, Child, SHORTCARD) INTEGER
   class, Selector, i RETURN _ :-
{  WITH Selector^.Child DO
      RETURN class^.Class.BitCount + BitOffset +
	 Class^.Class.BitIndex^ [i - class^.Class.AttrCount - InstOffset].ToBit - 1;
   END;
}; .

FUNCTION ToAttr (Class, INTEGER) INTEGER
   LOCAL { VAR a: SHORTCARD; }
   class, i RETURN _ :-
{  WITH class^.Class DO
      FOR a := AttrCount + 1 TO InstCount DO
	 WITH Instance^ [a] DO
	    IF ({Input, Test, Dummy} * Properties = {}) AND
	       (ToBit2 (class, Selector, a) = i) THEN RETURN a; END;
	 END;
      END;
   END;
   RETURN 0;
}; .

PROCEDURE GenEvaluator (t: Tree)

Assign (..) ;
Copy (..) :-
   GenEvaluator (Arguments);
   .
TargetCode (..) :-
   GenEvaluator (Code);
   .
Check (..) :-
   GenEvaluator (Condition);
   GenEvaluator (Statement);
   GenEvaluator (Actions);
   .
Designator (..) ;
Ident (..) ;
Any (..) ;
Anys (..) :-
   GenEvaluator (Next);
   .
Remote (..) :-
   TheClass: Class; k: INTEGER;
   TheClass := IdentifyClass (TreeRoot^.Ag.Classes, Type);
{  IF TheClass # NoTree THEN
      Attr := IdentifyAttribute (TheClass, Attribute);
      IF Attr # NoTree THEN
	 WITH Attr^.Attribute DO
	    k := ToBit0 (TheClass, AttrIndex);
	    IF Synthesized IN Properties THEN
	       !IFNOTIN (! WN (k MOD BSS); !,! EvalMod.GenEvaluator (Designators);
	       !^.yyHead.yyIsComp! WN (k DIV BSS); !) yyS! WN (k); ! (! 
	       EvalMod.GenEvaluator (Designators); !); END;!
	    ELSIF Inherited IN Properties THEN
	       !IFNOTIN (! WN (k MOD BSS); !,! EvalMod.GenEvaluator (Designators);
	       !^.yyHead.yyIsComp! WN (k DIV BSS); !) yyI [! 
	       EvalMod.GenEvaluator (Designators); !^.yyHead.yyOffset + ! WN (k);
	       !](! EvalMod.GenEvaluator (Designators); !^.yyHead.yyParent); END;!
	    END;
	 END;
      END;
   END;
   GenEvaluator (Next);
}; .

PROCEDURE GenEvalAttrD (t: Tree)

Class (..) :-
	GenEvalAttrD (Attributes);
	 .
Child (..) ;
Attribute (..) :-
	CONDITION ({{Input, Test, Dummy, Virtual}} * Properties = {{}}) AND NOT IsElement (Name, EvalNames);
	Include (EvalNames,Name);
	!PROCEDURE Eval! WI (Name); !	(yyt: ! WI (iMain); !.! WI (itTree); !);!
	GenEvalAttrD (Next);
	.
Child (..) ;
Attribute (..) :-
	GenEvalAttrD (Next);
	.

PROCEDURE GenEvalAttrI (t: Tree)

Class (..) :-
	GenEvalAttrI (Attributes);
	 .
Child (..) ;
Attribute (..) :-
	CONDITION ({{Input, Test, Dummy, Virtual}} * Properties = {{}}) AND NOT IsElement (Name, EvalNames);
	Include (EvalNames,Name);
	!PROCEDURE Eval! WI (Name); ! (yyt: ! WI (iMain); !.! WI (itTree); !);!
	! BEGIN!
	IsFirst := TRUE;
	gIdent  := Name;
	GenEvalAttr (TreeRoot^.Ag.Classes);
{	IF NOT IsFirst THEN !END;!
	END;
};
	! END Eval! WI (Name); !;!
	!!
	GenEvalAttrI (Next);
	.
Child (..) ;
Attribute (..) :-
	GenEvalAttrI (Next);
	.

PROCEDURE GenEvalAttr (t: Tree)

Class (..) :-
{	n := 0;
	j := 2;
	LOOP
	   IF j > AttrCount THEN EXIT; END;
	   WITH Instance^ [j] DO
	      IF NOT (Input IN Properties) AND (Attribute^.Child.Name = gIdent) THEN
		 INC (n); EXIT;
	      END;
	   END;
	   INC (j);
	END;
	IF n > 0 THEN
	   IF IsFirst THEN IsFirst := FALSE; ELSE !ELS! END;
	   IF Extensions^.Kind = Tree.NoClass THEN	(* Low ? *)
	      !IF yyt^.Kind = ! WI (iMain); !.! WI (Name); ! THEN!
	   ELSE
	      !IF IsType (yyt, ! WI (iMain); !.! WI (Name); !) THEN!
	   END;
	   Class := t;
	   GenCall (t, j);
	ELSE
	   GenEvalAttr (Extensions);
	END;
};
	GenEvalAttr (Next);
	.
