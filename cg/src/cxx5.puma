/* Ich, Doktor Josef Grosch, Informatiker, 27.3.1992 */

TRAFO	EvalCxx3
TREE	Tree
PUBLIC	EvalImplCxx EvalDefCxx ToBit0

GLOBAL {

FROM SYSTEM	IMPORT ADR, TSIZE;
FROM General	IMPORT Max;
FROM DynArray	IMPORT MakeArray;
FROM IO		IMPORT WriteS, WriteNl, WriteI, WriteB, StdOutput;
FROM Idents	IMPORT tIdent, MaxIdent;
FROM Texts	IMPORT WriteText;
FROM Sets	IMPORT tSet, MakeSet, ReleaseSet, Include, Exclude, Minimum,
			Maximum, IsElement, WriteSet, IsEmpty, Extract;
FROM Relation	IMPORT IsRelated;
FROM TreeCxx1	IMPORT BSS;
FROM TreeCxx2	IMPORT GetIterator, Iterator, WriteLine;
FROM EvalCxx	IMPORT Class, WC;
FROM Errors	IMPORT Error, Short, MessageI;
FROM Position	IMPORT NoPosition;
IMPORT EvalCxx;

FROM Tree	IMPORT
   NoTree	, tTree		, NoCodeClass	, Options	,
   Computed	, Reverse	, Write		, Read		,
   Inherited	, Synthesized	, Input		, Output	,
   Virtual	, Test		, Left		, Right		,
   HasOutput	, NonBaseComp	, Dummy		, Trace		,
   TreeRoot	, iMain		, itTree	, ErrorCount	,
   ForallClasses, ForallAttributes, f		, WI, WN	,
   ClassCount	, IdentifyClass	, IdentifyAttribute, 
   tBitIndex	, tBitInfo	, iNoTree	, QueryTree	;

VAR
   i, i2, j, k, n, MaxBit, MaxInstCount, Check: SHORTCARD;
   Node			: tTree;
   Success, IsStable, IsFirst	: BOOLEAN;
   BitIndexSize		: LONGINT;
   gBitIndex		: tBitIndex;
   InhIndices, EvalNames: tSet;
   InhIndexSize		: LONGINT;
   InhIndexCount	: POINTER TO ARRAY [1..1000000] OF SHORTCARD;
   gIdent		: tIdent;

PROCEDURE GenCall (t: tTree; j: SHORTCARD);
   BEGIN
      WITH t^.Class.Instance^ [j] DO
	 IF ({Synthesized, Left} <= Properties) THEN
	    k := ToBit0 (t, j);
	    !IFNOTIN (! WN (k MOD BSS); !, yyt->yyIsComp! WN (k DIV BSS); !) {! 
	    ! yyS! WN (k); ! (yyt); /* ! WI (Attribute^.Child.Name); ! */ }!
	 ELSIF ({Inherited, Left} <= Properties) THEN
	    k := ToBit0 (t, j);
	    !IFNOTIN (! WN (k MOD BSS); !, yyt->yyIsComp! WN (k DIV BSS); !) {! 
   IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	    ! yyVisitParent (yyt);! 
	    ! yyI [yyt->yyOffset + ! WN (k); !](yyt->yyParent); /* ! 
	    WI (Attribute^.Child.Name); ! */! 
	    @ yyWriteVisit (yyt->yyParent, "?"); }@
   ELSE
	    ! yyI [yyt->yyOffset + ! WN (k); !](yyt->yyParent); /* ! 
	    WI (Attribute^.Child.Name); ! */ }!
   END;
	 ELSIF ({Inherited, Right} <= Properties) THEN
	    k := ToBit1 (Selector, j - t^.Class.AttrCount - Selector^.Child.InstOffset);
	    !IFNOTIN (! WN (k MOD BSS); !, (! WC (Class^.Class.Name); !yyt)->! 
	    WI (Selector^.Child.Name); !->yyIsComp! WN (k DIV BSS); !) {! 
	    k := ToBit2 (t, Selector, j);
	    ! yyI! WN (k); ! (yyt); /* ! WI (Selector^.Child.Name);
	    !:! WI (Attribute^.Child.Name); ! */ }!
	 ELSIF ({Synthesized, Right} <= Properties) THEN
	    k := ToBit1 (Selector, j - t^.Class.AttrCount - Selector^.Child.InstOffset);
	    !IFNOTIN (! WN (k MOD BSS); !, (! WC (Class^.Class.Name); !yyt)->! 
	    WI (Selector^.Child.Name); !->yyIsComp! WN (k DIV BSS); !) {! 
   IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	    @ yyWriteVisit (yyt, "@ WI (Selector^.Child.Name); @");@ 
	    ! yyS! WN (k);
	    ! ((! WC (Class^.Class.Name); !yyt)->! WI (Selector^.Child.Name);
	    !); /* ! WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); ! */! 
	    ! yyVisitParent ((! WC (Class^.Class.Name); !yyt)->! WI (Selector^.Child.Name); !); }!
   ELSE
	    ! yyS! WN (k);
	    ! ((! WC (Class^.Class.Name); !yyt)->! WI (Selector^.Child.Name);
	    !); /* ! WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); ! */ }!
   END;
	 END;
      END;
   END GenCall;

PROCEDURE GenCompAttr (t: tTree; i: INTEGER);
   BEGIN
      Class := t;
      WITH t^.Class.Instance^ [i] DO
   IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
	 @yyWriteEval (yyt, "@ WI (Attribute^.Child.Name); @");@
	 IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
	    EvalCxx.GenEvaluator (Action); !!
	    IF Test IN Properties THEN
	       !writebool (yyb) xxWriteNl ();!
	    ELSIF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
	       !write! WI (itTree);
	       ! ((! WC (t^.Class.Name); !yyt)->! WI (Attribute^.Child.Name); !) xxWriteNl ();!
	    ELSE
	       !write! WI (Attribute^.Child.Type);
	       ! ((! WC (t^.Class.Name); !yyt)->! WI (Attribute^.Child.Name); !) xxWriteNl ();!
	    END;
	 ELSE
	    !xxWriteNl ();!
	 END;
   ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
	 @yyWriteEval (yyt, "@ WI (Attribute^.Child.Name); @");@
	 IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
	    EvalCxx.GenEvaluator (Action);
	 END;
   ELSE
	 IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
	    EvalCxx.GenEvaluator (Action);
	 END;
   END;
      END;
   END GenCompAttr;
}

PROCEDURE EvalDefCxx (t: Tree)
	
Ag (..) :- {
	!# ifndef yy! WI (EvalName); !!
	!# define yy! WI (EvalName); !!
	!!
	@# include "@ WI (iMain); @.h"@
	!!
	WriteLine (EvalCodes^.Codes.ImportLine);
	WriteText (f, EvalCodes^.Codes.Import);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.ImportLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Import);
	   Node := Node^.Module.Next;
	END;
	WriteLine (EvalCodes^.Codes.ExportLine);
	WriteText (f, EvalCodes^.Codes.Export);
	Node := Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.ExportLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Export);
	   Node := Node^.Module.Next;
	END;
	!!
	!extern void ! WI (EvalName); ! (! WI (itTree); ! yyt);!
	MakeSet (EvalNames, MaxIdent ());
	ForallClasses (Classes, GenEvalAttrD);
	ReleaseSet (EvalNames);
	!extern void Begin! WI (EvalName); ! ();!
	!extern void Close! WI (EvalName); ! ();!
	!!
	!# endif!
}; .

PROCEDURE EvalImplCxx (t: Tree)
	
Ag (..) :- {
	MaxBit := 0;
	MaxInstCount := 0;
	ForallClasses (Classes, CompBitInfo);
	MakeSet (InhIndices, MaxInstCount);
	InhIndexSize := MaxInstCount;
	MakeArray (InhIndexCount, InhIndexSize, TSIZE (SHORTCARD));
	FOR i := 1 TO MaxInstCount DO InhIndexCount^ [i] := 0; END;
	ForallClasses (Classes, CompInhIndices);
	@# define IFNOTIN(b, s) if (! (s & 1 << b))@
	!# define INCL(s, b) s |= 1 << b!
	!# define REMOTE_SYN(i, b, c, n, t, a) (n->i & 1 << b ? (void) 0 : c (n), ((class ! WI (iMain); !::t *) n)->a)!
	!# define REMOTE_INH(i, b, k, n, t, a) (n->i & 1 << b ? (void) 0 : yyI [n->yyOffset + k](n->yyParent), ((class ! WI (iMain); !::t *) n)->a)!
	EvalCxx.EvalImplHead (t);
	!static char * yyCheck2 = & generate_! WI (iMain); !_module_with_option_L;!
	!!
	!static void yyE (register ! WI (itTree); ! yyt);!
	FOR i := 1 TO MaxBit - 1 DO
	   !static void yyS! WN (i); ! (register ! WI (itTree); ! yyt);!
	END;
	FOR i := Minimum (InhIndices) TO Maximum (InhIndices) DO
	   IF IsElement (i, InhIndices) THEN
	      !static void yyI! WN (i); ! (register ! WI (itTree); ! yyt);!
	   END;
	END;
	!!
	!static const ! WI (iMain); !::yytProcTree yyI [! WN (Maximum (InhIndices) + 1); !] = { 0,!
	FOR i := 1 TO Maximum (InhIndices) DO
	   IF IsElement (i, InhIndices) THEN
	      ! yyI! WN (i); !,!
	   ELSE
	      ! 0,!
	   END;
	END;
	!};!
	!!
	!static void yyAbort (! WI (itTree); ! yyt)!
	!{!
	! ! WI (iMain); !::ErrorsObj->ErrorMessageI (xxCyclicDependencies, xxFatal, NoPosition,! 
	@ xxString, "@ WI (EvalName); @");@
	! throw ! WI (iMain); !::ErrorsObj->CodeToText (xxCyclicDependencies);!
	!}!
	!!
	!void ! WI (EvalName); ! (! WI (itTree); ! yyt)!
	!{!
      IF IsElement (ORD ('9'), Options) THEN
	! char yyHigh;!
	! yyMinLow = & yyHigh;!
      END;
      IF IsElement (ORD ('X'), Options) THEN
	! if (yyf == NULL) yyf = stdout;!
      END;
	! ! WI (iMain); !::yyInit (yyt); yyE (yyt);!
      IF IsElement (ORD ('9'), Options) THEN
	@ printf ("Stacksize %ld\n", (long) (& yyHigh - yyMinLow));@
      END;
	!}!
	!!
	REPEAT IsStable := TRUE; ForallClasses (Classes, CompOutput); UNTIL IsStable;
	!static void yyE (register ! WI (itTree); ! yyt)!
	!{!
	WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	Node := TreeRoot^.Ag.Modules;
	WHILE Node^.Kind = Tree.Module DO
	   WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
	   WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
	   Node := Node^.Module.Next;
	END;
      IF IsElement (ORD ('9'), Options) THEN
	! char yyLow;!
	! if (& yyLow < yyMinLow) yyMinLow = & yyLow;!
      END;
	! for (;;) {!
	!  if (yyt == ! WI (iNoTree); ! || yyt->yyIsComp0 & 1) return;!
	!  yyt->yyIsComp0 |= 1;!
	!  switch (yyt->Kind) {!
	ForallClasses (Classes, GenE);
	!  default: return;!
	!  }!
	! }!
	!}!
	!!
	MakeSet (EvalNames, MaxIdent ());
	ForallClasses (Classes, GenEvalAttrI);
	ReleaseSet (EvalNames);
	FOR i := 2 TO MaxBit DO
	   n := 0;			(* are there any SYN attributes ? *)
	   ForallClasses (Classes, CountSynAttr);
	   IF n > 0 THEN
	      !static void yyS! WN (i - 1); ! (register ! WI (itTree); ! yyt)!
	      !{!
	      WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	      WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	      Node := TreeRoot^.Ag.Modules;
	      WHILE Node^.Kind = Tree.Module DO
		 WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
		 WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
		 Node := Node^.Module.Next;
	      END;
      IF IsElement (ORD ('9'), Options) THEN
	      ! char yyLow;!
	      ! if (& yyLow < yyMinLow) yyMinLow = & yyLow;!
      END;
      IF IsElement (ORD ('5'), Options) THEN
	      ! IFNOTIN (! WN ((i - 1) MOD BSS); !, yyt->yyIsDone! WN ((i - 1) DIV BSS);
	      !) { INCL (yyt->yyIsDone! WN ((i - 1) DIV BSS); !, ! WN ((i - 1) MOD BSS); !); } else yyAbort (yyt);!
      END;
	      IF n > 1 THEN
		 ! switch (yyt->Kind) {!
		 ForallClasses (Classes, GenS);
		 ! }!
	      ELSE
		 ForallClasses (Classes, GenS);
	      END;
	      ! INCL (yyt->yyIsComp! WN ((i - 1) DIV BSS); !, ! WN ((i - 1) MOD BSS); !);!
	      !}!
	      !!
	   END;
	END;
	FOR i := Minimum (InhIndices) TO Maximum (InhIndices) DO
	   IF IsElement (i, InhIndices) THEN
	      !static void yyI! WN (i); ! (register ! WI (itTree); ! yyt)!
	      !{!
	      WriteLine (TreeRoot^.Ag.EvalCodes^.Codes.LocalLine);
	      WriteText (f, TreeRoot^.Ag.EvalCodes^.Codes.Local);
	      Node := TreeRoot^.Ag.Modules;
	      WHILE Node^.Kind = Tree.Module DO
		 WriteLine (Node^.Module.EvalCodes^.Codes.LocalLine);
		 WriteText (f, Node^.Module.EvalCodes^.Codes.Local);
		 Node := Node^.Module.Next;
	      END;
      IF IsElement (ORD ('9'), Options) THEN
	      ! char yyLow;!
	      ! if (& yyLow < yyMinLow) yyMinLow = & yyLow;!
      END;
	      Check := 0;
	      IF InhIndexCount^ [i] > 1 THEN
		 ! switch (yyt->Kind) {!
		 ForallClasses (Classes, EvalImplCxx);
		 ! }!
	      ELSE
		 ForallClasses (Classes, EvalImplCxx);
	      END;
	      IF Check # InhIndexCount^ [i] THEN
	         MessageI ("internal error in yyI", Error, NoPosition, Short, ADR (i));
		 INC (ErrorCount);
	      END;
	      !}!
	      !!
	   END;
	END;
	EvalCxx.EvalImplTail (t);
}; .
Class (..) :-
	CONDITION NoCodeClass * Properties = {{}};
	CONDITION i <= InstCount;
	a: SHORTCARD;
{	a := ToAttr (t, i);
	IF a = 0 THEN RETURN; END;
	WITH Instance^ [a] DO
	   IF {Inherited, Right} <= Properties THEN
	      Class := t;
	      IF InhIndexCount^ [i] > 1 THEN
		 !  case k! WI (Name); !:!
	      END;
	      INC (Check);
	      k := ToBit1 (Selector, a - AttrCount - Selector^.Child.InstOffset);
      IF IsElement (ORD ('5'), Options) THEN
	      ! IFNOTIN (! WN (k MOD BSS); !, (! WC (Class^.Class.Name); !yyt)->! 
	      WI (Selector^.Child.Name); !->yyIsDone! WN (k DIV BSS);
	      !) { INCL ((! WC (Class^.Class.Name); !yyt)->! WI (Selector^.Child.Name);
	      !->yyIsDone! WN (k DIV BSS); !, ! WN (k MOD BSS); !); } else yyAbort (yyt);!
      END;
	      FOR j := 1 TO InstCount DO
		 IF IsRelated (a, j, DP) THEN
		    GenCall (t, j);
		 END;
	      END;
      IF IsElement (ORD ('X'), Options) AND (Trace IN t^.Class.Properties) THEN
	      @yyWriteEval (yyt, "@ WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); @");@
	      IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
		 EvalCxx.GenEvaluator (Action); !!
		 IF (Attribute^.Kind = Tree.Child) OR (Attribute^.Attribute.Type = itTree) THEN
		    !write! WI (itTree);
		 ELSE
		    !write! WI (Attribute^.Child.Type);
		 END;
		 ! ((! WC (Name); !yyt)->! WI (Selector^.Child.Name);
		 !->! WI (Attribute^.Child.Name); !) xxWriteNl ();!
	      ELSE
		 !xxWriteNl ();!
	      END;
      ELSIF IsElement (ORD ('Y'), Options) AND (Trace IN t^.Class.Properties) THEN
	      @yyWriteEval (yyt, "@ WI (Selector^.Child.Name); !:! WI (Attribute^.Child.Name); @");@
	      IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
		 EvalCxx.GenEvaluator (Action);
	      END;
      ELSE
	      IF (Action # tTree (ADR (Action))) AND NOT (Virtual IN Properties) THEN
		 EvalCxx.GenEvaluator (Action);
	      END;
      END;
	      IF NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) THEN
		 !{ register ! WI (itTree); ! yyr = (! WC (Name); !yyt)->! 
		 WI (Selector^.Child.Name); !->! WI (Attribute^.Child.Name);
		 !; if (yyr->yyParent == ! WI (iNoTree);
		 !) { yyr->yyOffset = ! WN (Selector^.Child.Class^.Class.BitCount + Attribute^.Child.BitOffset);
		 !; yyr->yyParent = (! WC (Name); !yyt)->! WI (Selector^.Child.Name);
		 !; ! WI (iMain); !::yyInit (yyr); } }!
	      END;
	      FOR i2 := 1 TO InstCount DO	(* add group members *)
		 IF Instance^[i2].Action = Action THEN
		    WITH Instance^[i2] DO
		       IF Synthesized IN Properties THEN
			  k := ToBit0 (Class, i2);
			  !  INCL (yyt->yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
		       ELSIF Inherited IN Properties THEN
			  k := ToBit1 (Selector, i2 - AttrCount - Selector^.Child.InstOffset);
			  !  INCL ((! WC (Class^.Class.Name); !yyt)->! WI (Selector^.Child.Name);
			  !->yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
		       END;
		    END;
		 END;
	      END;
	      ! return;!
	   END;
	END;
}; .

PROCEDURE CompBitInfo (t: Tree)

Class (..) :-
	BitIndexSize := AttrCount;
	MakeArray (BitIndex, BitIndexSize, TSIZE (tBitInfo));
	i := 1;
	gBitIndex := BitIndex;
	ForallAttributes (t, CompBitInfo);
	MaxBit := Max (i, MaxBit);
	MaxInstCount := Max (InstCount, MaxInstCount);
	.
Child (..) ;
Attribute (..) :-
	CONDITION {{Input, Test, Dummy}} * Properties = {{}};
	INC (i);
	gBitIndex^ [AttrIndex].ToBit := i;
	gBitIndex^ [i].ToAttr := AttrIndex;
	.

PROCEDURE CompInhIndices (t: Tree)

Class (..) :-
	b: INTEGER;
{	FOR j := AttrCount + 1 TO InstCount DO
	   WITH Instance^ [j] DO
	      IF Inherited IN Properties THEN
		 b := ToBit2 (t, Selector, j);
		 Include (InhIndices, b);
		 INC (InhIndexCount^ [b]);
	      END;
	   END;
	END;
};	.

PROCEDURE CountSynAttr (t: Tree)

Class (..) :-
	CONDITION NoCodeClass * Properties = {{}};
	CONDITION i <= BitCount;
{	WITH Instance^ [BitIndex^ [i].ToAttr] DO
	   IF ({Synthesized, Left} <= Properties) AND NOT (Test IN Properties) THEN
	      INC (n);
	   END;
	END;
};	.

PROCEDURE GenS (t: Tree)

Class (..) :-
	CONDITION NoCodeClass * Properties = {{}};
	CONDITION i <= BitCount;
{	WITH Instance^ [BitIndex^ [i].ToAttr] DO
	   IF ({Synthesized, Left} <= Properties) AND NOT (Test IN Properties) THEN
	      Class := t;
	      IF n > 1 THEN
		 ! case k! WI (Name); !:!
	      END;
	      FOR j := 1 TO InstCount DO
		 IF IsRelated (BitIndex^ [i].ToAttr, j, DP) THEN
		    GenCall (t, j);
		 END;
	      END;
	      GenCompAttr (t, BitIndex^ [i].ToAttr);
	      IF NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) THEN
		 !{ register ! WI (itTree); ! yyr = (! WC (Name); !yyt)->! WI (Attribute^.Child.Name);
		 !; if (yyr->yyParent == ! WI (iNoTree);
		 !) { yyr->yyOffset = ! WN (BitCount + Attribute^.Child.BitOffset);
		 !; yyr->yyParent = yyt; ! WI (iMain); !::yyInit (yyr); } }!
	      END;
	      FOR i2 := 1 TO InstCount DO	(* add group members *)
		 IF Instance^[i2].Action = Action THEN
		    WITH Instance^[i2] DO
		       IF Synthesized IN Properties THEN
			  k := ToBit0 (Class, i2);
			  IF k # i - 1 THEN
			     !  INCL (yyt->yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
			  END;
		       ELSIF Inherited IN Properties THEN
			  k := ToBit1 (Selector, i2 - AttrCount - Selector^.Child.InstOffset);
			  !  INCL ((! WC (Class^.Class.Name); !yyt)->! WI (Selector^.Child.Name);
			  !->yyIsComp! WN (k DIV BSS); !, ! WN (k MOD BSS); !);!
		       END;
		    END;
		 END;
	      END;
	      IF n > 1 THEN
		 !  break;!
	      END;
	   END;
	END;
}; .

PROCEDURE GenE (t: Tree)

Class (..) :-
	ToCompute: tSet;
{	GetIterator (t);
	n := 0;
	j := 2;
	LOOP
	   IF j > InstCount THEN EXIT; END;
	   WITH Instance^ [j] DO
	      IF {Dummy, Output, Test} * Properties # {} THEN
	         IF (Test IN Properties) OR
		    ({Synthesized, Left} <= Properties) OR
		    ({Inherited,  Right} <= Properties) OR
		    ({Inherited,   Left} <= Properties) AND
		    NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) OR
		    ({Synthesized, Right, Dummy} <= Properties) AND (Selector # Iterator) AND
		    (HasOutput IN Selector^.Child.Class^.Class.Properties) THEN
		    INC (n); EXIT;
		 END;
	      END;
	   END;
	   INC (j);
	END;
	IF (n = 0) AND ((Iterator = NoTree) OR NOT (HasOutput IN Iterator^.Child.Class^.Class.Properties)) THEN RETURN; END;

	Class := t;
	!  case k! WI (Name); !:!
	FOR j := 2 TO InstCount DO
	   WITH Instance^ [j] DO
	      IF {Dummy, Output} * Properties # {} THEN
		 IF ({Synthesized, Left} <= Properties) OR
		    ({Inherited,  Right} <= Properties) OR
		    ({Inherited,   Left} <= Properties) AND
		    NOT (Input IN Properties) AND (Attribute^.Kind = Tree.Child) THEN
		    GenCall (t, j);
		 ELSIF ({Synthesized, Right, Dummy} <= Properties) AND (Selector # Iterator) AND
		    (HasOutput IN Selector^.Child.Class^.Class.Properties) THEN
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
		    @yyWriteVisit (yyt, "@ WI (Selector^.Child.Name); @"); @ 
      END;
		    !yyE ((! WC (Name); !yyt)->! WI (Selector^.Child.Name); !);!
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
		    !yyVisitParent ((! WC (Name); !yyt)->! WI (Selector^.Child.Name); !);!
      END;
		 END;
	      END;
	   END;
	END;

	MakeSet (ToCompute, InstCount);
	FOR i := 2 TO AttrCount DO
	   WITH Instance^ [i] DO
	      IF Test IN Properties THEN
		 FOR j := 2 TO InstCount DO
		    IF IsRelated (i, j, DP) THEN
		       IF {Synthesized, Inherited} * Instance^ [j].Properties # {} THEN
			  Include (ToCompute, j);
		       END;
		    END;
		 END;
	      END;
	   END;
	END;
	FOR i := 2 TO InstCount DO
	   WITH Instance^ [i] DO
	      IF ({Synthesized, Left, Output} <= Properties) OR
		 ({Inherited,  Right, Output} <= Properties) THEN
		 Exclude (ToCompute, i);
	      END;
	   END;
	END;
	WHILE NOT IsEmpty (ToCompute) DO
	   GenCall (t, Extract (ToCompute));
	END;
	ReleaseSet (ToCompute);
	FOR i := 2 TO AttrCount DO
	   IF Test IN Instance^ [i].Properties THEN
	      GenCompAttr (t, i);
	   END;
	END;

	IF (Iterator = NoTree) OR NOT (HasOutput IN Iterator^.Child.Class^.Class.Properties) THEN
	   !return;!
	ELSE
      IF IsElement (ORD ('Z'), Options) AND (Trace IN t^.Class.Properties) THEN
	   @yyWriteVisit (yyt, "@ WI (Iterator^.Child.Name); @"); @ 
      END;
	   !yyt = (! WC (Name); !yyt)->! WI (Iterator^.Child.Name); !; break;!
	END;
}; .

PROCEDURE CompOutput (t: Tree)

Class (..) :-
   CONDITION NOT (HasOutput IN Properties);
   Success := FALSE;
   ForallAttributes (t, CompOutput);
   ForallClasses (Extensions, CompOutput2);
   CONDITION Success;
   INCL (Properties, HasOutput);
   IsStable := FALSE;
   .
Child (..) :-
   CONDITION (Output IN Properties) OR (HasOutput IN Class^.Class.Properties);
   Success := TRUE;
   .
Attribute (..) :-
   CONDITION {{Test, Output}} * Properties # {{}};
   Success := TRUE;
   .

PROCEDURE CompOutput2 (t: Tree)

Class (..) :-
   CONDITION HasOutput IN Properties;
   Success := TRUE;
   .

FUNCTION ToBit0 (Class, INTEGER) INTEGER
   class, i :- RETURN class^.Class.BitIndex^ [i].ToBit - 1; .

FUNCTION ToBit1 (Child, INTEGER) INTEGER
   Selector, i :- RETURN Selector^.Child.Class^.Class.BitIndex^ [i].ToBit - 1; .

FUNCTION ToBit2 (Class, Child, SHORTCARD) INTEGER
   class, Selector, i RETURN _ :-
{  WITH Selector^.Child DO
      RETURN class^.Class.BitCount + BitOffset +
	 Class^.Class.BitIndex^ [i - class^.Class.AttrCount - InstOffset].ToBit - 1;
   END;
}; .

FUNCTION ToAttr (Class, INTEGER) INTEGER
   LOCAL { VAR a: SHORTCARD; }
   class, i RETURN _ :-
{  WITH class^.Class DO
      FOR a := AttrCount + 1 TO InstCount DO
	 WITH Instance^ [a] DO
	    IF ({Input, Test, Dummy} * Properties = {}) AND
	       (ToBit2 (class, Selector, a) = i) THEN RETURN a; END;
	 END;
      END;
   END;
   RETURN 0;
}; .

PROCEDURE GenEvalAttrD (t: Tree)

Class (..) :-
	GenEvalAttrD (Attributes);
	 .
Child (..) ;
Attribute (..) :-
	CONDITION ({{Input, Test, Dummy, Virtual}} * Properties = {{}}) AND NOT IsElement (Name, EvalNames);
	Include (EvalNames, Name);
	!extern void ! WI (TreeRoot^.Ag.EvalName); WI (Name); !	(register ! WI (itTree); ! yyt);!
	GenEvalAttrD (Next);
	.
Child (..) ;
Attribute (..) :-
	GenEvalAttrD (Next);
	.

PROCEDURE GenEvalAttrI (t: Tree)

Class (..) :-
	GenEvalAttrI (Attributes);
	 .
Child (..) ;
Attribute (..) :-
	CONDITION ({{Input, Test, Dummy, Virtual}} * Properties = {{}}) AND NOT IsElement (Name, EvalNames);
	Include (EvalNames, Name);
	!void ! WI (TreeRoot^.Ag.EvalName); WI (Name); ! (register ! WI (itTree); ! yyt)!
	!{!
	IsFirst := TRUE;
	gIdent  := Name;
	GenEvalAttr (TreeRoot^.Ag.Classes);
	IF NOT IsFirst THEN ! }!
	END
	!}!
	!!
	GenEvalAttrI (Next);
	.
Child (..) ;
Attribute (..) :-
	GenEvalAttrI (Next);
	.

PROCEDURE GenEvalAttr (t: Tree)

Class (..) :-
{	n := 0;
	j := 2;
	LOOP
	   IF j > AttrCount THEN EXIT; END;
	   WITH Instance^ [j] DO
	      IF NOT (Input IN Properties) AND (Attribute^.Child.Name = gIdent) THEN
		 INC (n); EXIT;
	      END;
	   END;
	   INC (j);
	END;
	IF n > 0 THEN
	   IF IsFirst THEN IsFirst := FALSE; ELSE ! } else! END;
	   IF Extensions^.Kind = Tree.NoClass THEN	(* Low ? *)
	      ! if (yyt->Kind == k! WI (Name); !) {!
	   ELSE
	      ! if (! WI (iMain); !::IsType (yyt, k! WI (Name); !)) {!
	   END;
	   Class := t;
	   GenCall (t, j);
	ELSE
	   GenEvalAttr (Extensions);
	END;
};
	GenEvalAttr (Next);
	.
